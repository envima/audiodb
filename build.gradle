// gradle version 9.1.0, in eclipse gradle version 9.1.0 

plugins { 
  id "com.github.ben-manes.versions" version '0.53.0' // update check task: dependencyUpdates  https://github.com/ben-manes/gradle-versions-plugin
}

/*
check for updates:
gradle dependencyUpdates
or
./gradlew dependencyUpdates
*/

/*
list (transitive) dependencies
gradle dependencies
or
./gradlew dependencies
*/

// upgrade gradle wrapper
//./gradlew wrapper --gradle-version=9.1.0

def isNonStable = { String version ->
  return version.contains('alpha') || version.contains('beta') 
}

dependencyUpdates {
  rejectVersionIf {  // reject all non stable versions
    isNonStable(it.candidate.version)
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'

compileJava.options.encoding = 'UTF-8'

java {
  sourceCompatibility = '17' // build compatiblity with java 11 and newer
  targetCompatibility = '17' // run compatiblity with java 11 and newer
}

repositories {
    mavenCentral()
    
    /* server not always reachable
    maven { // needed for compile group: 'com.github.aelstad', name: 'keccakj', version: '1.1.0'
      url 'https://repository.mulesoft.org/nexus/content/repositories/public'
    }*/
    
    flatDir { // local jar files
       dirs 'lib'
   	}
}

dependencies {
	implementation 'org.tinylog:tinylog-impl:2.8.0-M1'
	runtimeOnly 'org.tinylog:slf4j-tinylog:2.8.0-M1'
  	
	implementation 'org.eclipse.jetty:jetty-server:11.0.26' // v12.x not compatible with Java 11
	implementation 'org.eclipse.jetty:jetty-security:11.0.26'
	implementation 'org.eclipse.jetty.http2:http2-server:11.0.26'
	implementation 'org.eclipse.jetty:jetty-alpn-java-server:11.0.26'
	
	implementation 'org.json:json:20250517'
	implementation 'org.yaml:snakeyaml:1.33'  // TODO: check compatibility with newer version 2.x
	implementation 'de.siegmar:fastcsv:3.3.1'
	implementation 'com.opencsv:opencsv:5.12.0'  
	implementation 'ch.randelshofer:fastdoubleparser:2.0.1'	
	
	implementation 'com.github.wendykierp:JTransforms:3.1'
	
	implementation 'com.samskivert:jmustache:1.16'

	//implementation 'com.github.aelstad:keccakj:1.1.0' // server not always reachable	
	implementation ':keccakj-1.1.0' // local jar file
	
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5' // breaking changes in 0.12.x
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	
	implementation 'com.webauthn4j:webauthn4j-core:0.25.0.RELEASE'
	
	implementation 'ar.com.hjg:pngj:2.1.0'	
	implementation 'org.imgscalr:imgscalr-lib:4.2'
	
	implementation 'com.drewnoakes:metadata-extractor:2.19.0'
	
	implementation 'com.h2database:h2:2.3.232' // old database v2.1.x files not compatible with v2.2.x
	
	implementation 'org.reflections:reflections:0.10.2'
	
	implementation 'org.lz4:lz4-java:1.8.0' // needed for xxHash
	
	//implementation 'fr.delthas:javamp3:1.0.1' // version with	IndexOutOfBoundsException bug
	implementation ':javamp3-1.0.4' // local jar file from  https://github.com/kevinstadler/JavaMP3
	
	//implementation 'net.jthink:jaudiotagger:3.0.1'
	
	implementation ':flac-library-java-2022-09-12' // local jar file derived from  https://github.com/nayuki/FLAC-library-Java		
}

sourceSets.main.java.srcDir 'src'
sourceSets.main.java.srcDir 'src_qoa'

jar {
	from file('src/tinylog.properties')
	
	destinationDirectory = project.layout.projectDirectory.dir('package')
		
	manifest {
		String classPathFiles = "";
		for(java.io.File file : files(configurations.runtimeClasspath)) {
			classPathFiles += "lib/"+file.getName()+" "; 
		}
		//println("the class path: "+classPathFiles);	
	
        attributes 	'Built-Date': new Date(),
					'Built-JDK': System.getProperty('java.version'),
					'Main-Class': 'run.Terminal',
					'Class-Path': classPathFiles
    }
}

tasks.withType(JavaCompile){
  //options.deprecation = true
  //options.listFiles = true
  //options.verbose = true
}

task clean_package(type: Delete) {
  delete 'package'
}

task copy_lib(type: Copy) {
	from files(configurations.runtimeClasspath)
    into 'package/lib'
}

task copy_add(type: Copy) {
	from fileTree('add')
	into 'package'
}

task copy_webcontent(type: Copy) {
	from fileTree('webcontent')
	into 'package/webcontent'
}

task copy_mustache(type: Copy) {
	from fileTree('mustache')
	into 'package/mustache'
}

task _package(dependsOn:[ clean_package, jar, copy_lib, copy_webcontent, copy_mustache, copy_add ]) {
	group = 'project' 
}